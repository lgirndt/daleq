/*
 * Copyright 2012 brands4friends, Private Sale GmbH
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */



def integrationTestName = 'integrationTest'
def INTEGRATION_DBS = ['HSQLDB', 'H2', 'Mysql']
def INTEGRATION_PROPERTY_NAMES = ['mysql.url', 'mysql.user', 'mysql.password']

sourceSets {
    integrationTest {
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile project(':daleq-core')
    compile project(':daleq-spring')
    compile deps.springJdbc

    compile deps.springContext
    testCompile deps.springTest
    testCompile deps.hsqldb
    testCompile deps.h2
    testCompile deps.mysql
}

INTEGRATION_DBS.each { dbType ->
    def taskName = integrationTestName + dbType
    task(taskName, type: Test) {
        testClassesDir = sourceSets.integrationTest.output.classesDir
        classpath = sourceSets.integrationTest.runtimeClasspath

        // TODO
        // testReportDir = new File("${reporting.baseDir}/${testReportDirName}/${name}")
        // testResultsDir = new File("${buildDir}/${testResultsDirName}/${name}")

        doFirst {
            def newProperties = ['spring.profiles.active': dbType]
            INTEGRATION_PROPERTY_NAMES.each {
                String propertyName = 'integration.' + it
                String propertyValue = propertyOr(propertyName, null)
                newProperties[propertyName] = propertyValue
            }
            systemProperties(newProperties)
        }
    }
}

task integrationTest {
    String whichDBs = propertyOr('integration.db', 'HSQLDB')
    dependsOn whichDBs.split(',').collect { integrationTestName + it }
}

check.dependsOn integrationTest

idea {
    module {
        testSourceDirs = [file('src/integrationTest/java'), file('src/integrationTest/resources')]
    }
}
